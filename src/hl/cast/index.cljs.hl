(page "index.html"
      (:require [tailrecursion.hoplon.storage-atom :refer [local-storage]]
                [datascript :as d]
                [cljs.core.match]
                [cljs.core.async :as async :refer [<! >! put! chan]]
                [taoensso.sente :as sente :refer [cb-success?]])
      (:require-macros [cljs.core.match.macros :refer [match]]
                       [cljs.core.async.macros :as asyncm :refer (go go-loop)]))

; --------------------------
; Initialize a database
; --------------------------

(def db (cell nil))

(let [schema {:vote/user {:db/valueType :db.type/ref}
              :vote/feature {:db/valueType :db.type/ref}
              :feature/page {:db/valueType :db.type/ref}}]
  (let [conn (d/create-conn schema)]
    (d/listen! conn #(reset! db @conn))
    (defn transact! [tx-data] (d/transact! conn tx-data))))

; --------------------------
; Websocket set up
; --------------------------

(let [{:keys [chsk ch-recv send-fn state]} (sente/make-channel-socket! "/chsk"  {:type :auto})]
  (def chsk       chsk)
  (def ch-chsk    ch-recv) ; ChannelSocket's receive channel
  (def chsk-send! send-fn) ; ChannelSocket's send API fn
  (def chsk-state state))  ; Watchable, read-only atom

(go-loop []
         (when-let [[ev-id ev-data] (<! ch-chsk)]
           (match [ev-id ev-data]

                  [:chsk/state {:first-open? true}]
                  (println "Connection established")

                  [:chsk/recv ([:db/updates stmts] :seq)]
                  (transact! (into [] stmts))

                  :else
                  (println "Received " ev-id ev-data))
           (recur)))

; --------------------------
; Input Cells
; --------------------------

(defc current-user-id nil)

(defc current-page nil)

; --------------------------
; Derived state
; --------------------------

(defc= current-user-name
  (ffirst (d/q '[:find ?n
                 :in $ ?u
                 :where [?u :user/name ?n]] db current-user-id)))

(defc= logged-in? (not (nil? current-user-id)))

(defc= users
  (d/q '[:find ?e
         :in $
         :where
         [?e :user/name ?n]] db))

(defc= features
  (d/q '[:find ?e ?t ?d
         :in $ ?p
         :where
         [?e :feature/title ?t]
         [?e :feature/description ?d]
         [?e :feature/page ?p]] db current-page))

(defc= user-vote-counts
  (d/q '[:find ?f (count ?v)
         :in $ ?u ?p
         :where
         [?v :vote/feature ?f]
         [?v :vote/user ?u]
         [?f :feature/page ?p]] db current-user-id current-page))

(defc= total-vote-counts
  (d/q '[:find ?f (count ?v)
         :in $
         :where
         [?v :vote/feature ?f]
         [?f :feature/page ?p]] db current-page))

(defc= pages
  (d/q '[:find ?p ?n
         :in $
         :where
         [?p :page/name ?n]] db))

(defc= combined-counts
  (merge-with merge
              (into {} (for [[id c] user-vote-counts] [id {:user c}]))
              (into {} (for [[id c] total-vote-counts] [id {:total c}]))))

(defc= features-with-votes
  (into [] (for [[id t d] features]
             (let [c (merge {:user 0 :total 0} (combined-counts id))]
               [id t d (:total c) (:user c)]))))

(defc= sort-order
  (->> (map vector features-with-votes (iterate inc 0))
       (sort-by #(get-in % [0 3]))
       (reverse)
       (map #(get % 1))))

(defc= sorted-features
  (map #(get features-with-votes %) sort-order))

(defc= votes-remaining
  (let [max-votes (ffirst (d/q '[:find ?m
                                 :in $ ?u
                                 :where
                                 [?u :user/max-votes ?m]] db current-user-id))
        current-votes (ffirst (d/q '[:find (count ?v)
                                     :in $ ?u
                                     :where
                                     [?v :vote/user ?u]] db current-user-id))]
    (- max-votes current-votes)))

; --------------------------
; Commands
; --------------------------

(defn do-cmd! [cmd & args]
  (match [cmd args]

         [:page/set ([id] :seq)]
         (reset! current-page id)

         [:authentication/login ([username password] :seq)]
         (chsk-send! [:authentication/login [username password]] 8000
                     #(let [{u :user-id e :entities} %]
                        (transact! e)
                        (reset! current-user-id u)
                        (reset! current-page (ffirst @pages))))

         [:authentication/logout _]
         (do
           (reset! current-user-id nil)
           (reset! current-page nil))

         :else (chsk-send! [cmd args])))


; --------------------------
; Page
; --------------------------

(html
 (head
  (link :rel "stylesheet" :type "text/css" :href "css/main.css"))
 (body
  (div :class "ui inverted fixed blue main menu"
       (div :class "ui simple dropdown item" "CAST"
            (div :class "menu"
                 :do-toggle logged-in?
                 (loop-tpl :bindings [[id page-name] pages]
                           :reverse true
                           (div :class "item"
                                :on-click #(do-cmd! :page/set @id) page-name))))

       (div :class "right menu"
            :do-toggle logged-in?
            (div :class "item"
                 (text "~{votes-remaining} votes left"))
            (div :class "ui simple dropdown item"
                 (text "~{current-user-name}")
                 (div :class "menu"
                      (div :class "item" :on-click #(do-cmd! :authentication/logout) "Log out")))))

  (div :class "container"
       (div :class "ui form segment"
            :do-toggle (cell= (not logged-in?))
            (div :class "field"
                 (label "Username")
                 (div :class "ui left labeled icon input"
                      (input :type "text" :id "username" :placeholder "Username")
                      (i :class "user icon")
                      (div :class "ui corner label"
                           (i :class "icon asterisk"))))
            (div :class "field"
                 (label "Password")
                 (div :class "ui left labeled icon input"
                      (input :type "password" :id "password")
                      (i :class "lock icon")
                      (div :class"ui corner label"
                           (i :class "icon asterisk"))))
            (div :class "ui blue submit button"
                 :on-click #(do-cmd! :authentication/login (val-id "username") (val-id "password"))
                 "Login"))

       (div :do-toggle logged-in?
            (loop-tpl :bindings [[id title description votes my-votes] sorted-features]
                      (let [votes-available (cell= (> votes-remaining 0))
                            voted-for-this (cell= (> my-votes 0))]
                        (div :class "ui blue segment"
                             (h4 :class "ui left floated header" (text "~{title}"))
                             (div :class "ui mini buttons"
                                  :do-toggle logged-in?
                                  (div :class  (cell= {:ui true
                                                       :green true
                                                       :icon true
                                                       :button true
                                                       :monochrome (not voted-for-this)})
                                       (i :class "icon plus"
                                          :on-click #(when @votes-available (do-cmd! :vote/up @id @current-user-id))))
                                  (div :class (cell= {:ui true
                                                      :red true
                                                      :icon true
                                                      :button true
                                                      :monochrome (not voted-for-this)})
                                       (i :class "icon minus"
                                          :on-click #(do-cmd! :vote/down @id @current-user-id))))
                             (div :class "ui top right attached label" (text "~{votes}"))
                             (div :class "ui clearing divider")
                             (p (text "~{description}")))))))))

