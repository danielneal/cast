(page "index.html"
      (:require [tailrecursion.hoplon.storage-atom :refer [local-storage]]
                [datascript :as d]
                [cast.rpc :as rpc]
                [cljs.core.match])
      (:require-macros [cljs.core.match.macros :refer [match]]))


; --------------------------
; Stem state
; --------------------------

(let [schema {:vote/user {:db/valueType :db.type/ref}
              :vote/feature {:db/valueType :db.type/ref}
              :feature/page {:db/valueType :db.type/ref}}]
  (def db (cell= (d/with (d/empty-db schema) rpc/entities))))

; --------------------------
; Input Cells
; --------------------------

(defc current-user nil)

(defc current-page nil)

; --------------------------
; Derived state
; --------------------------

(defc= current-user-name
  (ffirst (d/q '[:find ?n
                 :in $ ?u
                 :where [?u :user/name ?n]] db current-user)))

(defc= logged-in? (not (nil? current-user)))

(defc= users
  (d/q '[:find ?e
         :in $
         :where
         [?e :user/name ?n]] db))

(defc= features
  (d/q '[:find ?e ?t ?d
         :in $ ?p
         :where
         [?e :feature/title ?t]
         [?e :feature/description ?d]
         [?e :feature/page ?p]] db current-page))

(defc= user-vote-counts
  (d/q '[:find ?f (count ?v)
         :in $ ?u ?p
         :where
         [?v :vote/feature ?f]
         [?v :vote/user ?u]
         [?f :feature/page ?p]] db current-user current-page))

(defc= total-vote-counts
  (d/q '[:find ?f (count ?v)
         :in $
         :where
         [?v :vote/feature ?f]
         [?f :feature/page ?p]] db current-page))

(defc= pages
  (d/q '[:find ?p ?n
         :in $
         :where
         [?p :page/name ?n]] db))

(defc= combined-counts
  (merge-with merge
              (into {} (for [[id c] user-vote-counts] [id {:user c}]))
              (into {} (for [[id c] total-vote-counts] [id {:total c}]))))

(defc= features-with-votes
  (into [] (for [[id t d] features]
             (let [c (merge {:user 0 :total 0} (combined-counts id))]
               [id t d (:total c) (:user c)]))))

(defc= sort-order
  (->> (map vector features-with-votes (iterate inc 0))
       (sort-by #(get-in % [0 3]))
       (reverse)
       (map #(get % 1))))

(defc= sorted-features
  (map #(get features-with-votes %) sort-order))

(defc= votes-remaining
  (let [max-votes (ffirst (d/q '[:find ?m
                                 :in $ ?u
                                 :where
                                 [?u :user/max-votes ?m]] db current-user))
        current-votes (ffirst (d/q '[:find (count ?v)
                                     :in $ ?u
                                     :where
                                     [?v :vote/user ?u]] db current-user))]
    (- max-votes current-votes)))

; --------------------------
; Commands
; --------------------------

(defn do-cmd! [cmd & args]
  (match [cmd args]
         [:set-page ([id] :seq)]
         (reset! current-page id)

         [:logout _]
         (when logged-in?
           (reset! current-page nil)
           (reset! current-user nil)
           (rpc/logout!))

         [:login _]
         (reset! current-user (ffirst @users))

         :else
         (apply (rpc/command cmd) args)))

; --------------------------
; Initialize
; --------------------------
(do-cmd! :get-entities)
(js/setInterval #(do-cmd! :get-entities) 5000)

; --------------------------
; Page
; --------------------------

(html
 (head
  (link :rel "stylesheet" :type "text/css" :href "css/main.css"))
 (body
  (div :class "ui inverted fixed black main menu"
       (div :class "ui simple dropdown item" "CAST"
            (div :class "menu"
                 :do-toggle logged-in?
                 (loop-tpl :bindings [[id page-name] pages]
                           :reverse true
                           (div :class "item"
                                :on-click #(do-cmd! :set-page @id) page-name))))

       (div :class "right menu"
            :do-toggle logged-in?
            (div :class "item"
                 (text "~{votes-remaining} votes left"))
            (div :class "ui simple dropdown item"
                 (text "~{current-user-name}")
                 (div :class "menu"
                      (div :class "item" :on-click #(do-cmd! :logout) "Log out")))))

  (div :class "container"
       (div :class "ui form segment"
            :do-toggle (cell= (not logged-in?))
            (div :class "field"
                 (label "Username")
                 (div :class "ui left labeled icon input"
                      (input :type "text" :id "username" :placeholder "Username")
                      (i :class "user icon")
                      (div :class "ui corner label"
                           (i :class "icon asterisk"))))
            (div :class "field"
                 (label "Password")
                 (div :class "ui left labeled icon input"
                      (input :type "password" :id "password")
                      (i :class "lock icon")
                      (div :class"ui corner label"
                           (i :class "icon asterisk"))))
            (div :class "ui teal submit button"
                 :on-click #(do-cmd! :login (val-id "username") (val-id "password"))
                 "Login"))

       (div :do-toggle logged-in?
            (loop-tpl :bindings [[id title description votes my-votes] sorted-features]
                      (let [votes-available (cell= (> votes-remaining 0))
                            voted-for-this (cell= (> my-votes 0))]
                        (div :class "feature ui inverted black segment"
                             (div :class "votes ui raised teal segment"
                                  (span (text "~{votes}")))
                             (div :class "details"
                                  (div :class "ui bottom right attached black inverted label"
                                       (div :class "ui mini buttons"
                                            (div :class "ui icon green button"
                                                 :on-click #(when @votes-available (do-cmd! :up-vote @id @current-user))
                                                 (i :class "icon plus")
                                                 (span :do-toggle voted-for-this
                                                       (text "~{my-votes}")))
                                            (div :class "ui icon red button"
                                                 :on-click #(do-cmd! :down-vote @id @current-user)
                                                 (i :class "icon minus"))))
                                  (div :class "ui header" (text "~{title}"))
                                  (div :class "ui clearing divider")
                                  (p (text "~{description}"))))))))))

