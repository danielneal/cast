(page "index.html"
      (:require [tailrecursion.hoplon.storage-atom :refer [local-storage]]
                [datascript :as d]
                [cast.rpc :as rpc]
                [cljs.core.match])
      (:require-macros [cljs.core.match.macros :refer [match]]))

; --------------------------
; Initialization
; --------------------------

(let [schema {:vote/user {:db/valueType :db.type/ref}
              :vote/feature {:db/valueType :db.type/ref}
              :feature/page {:db/valueType :db.type/ref}}
      conn (d/create-conn schema)]
  (def db (cell= (d/with (d/empty-db schema) rpc/entities)))
  (rpc/get-entities)
  (js/setInterval rpc/get-entities 5000))

; --------------------------
; Input Cells
; --------------------------

(defc current-user-id nil)

(defc current-page nil)

; --------------------------
; Derived state
; --------------------------

(defc= current-user-name
  (ffirst (d/q '[:find ?n
                 :in $ ?u
                 :where [?u :user/name ?n]] db current-user-id)))

(defc= logged-in? (not (nil? current-user-id)))

(defc= users
  (d/q '[:find ?e
         :in $
         :where
         [?e :user/name ?n]] db))

(defc= features
  (d/q '[:find ?e ?t ?d
         :in $ ?p
         :where
         [?e :feature/title ?t]
         [?e :feature/description ?d]
         [?e :feature/page ?p]] db current-page))

(defc= user-vote-counts
  (d/q '[:find ?f (count ?v)
         :in $ ?u ?p
         :where
         [?v :vote/feature ?f]
         [?v :vote/user ?u]
         [?f :feature/page ?p]] db current-user-id current-page))

(defc= total-vote-counts
  (d/q '[:find ?f (count ?v)
         :in $
         :where
         [?v :vote/feature ?f]
         [?f :feature/page ?p]] db current-page))

(defc= pages
  (d/q '[:find ?p ?n
         :in $
         :where
         [?p :page/name ?n]] db))

(defc= combined-counts
  (merge-with merge
              (into {} (for [[id c] user-vote-counts] [id {:user c}]))
              (into {} (for [[id c] total-vote-counts] [id {:total c}]))))

(defc= features-with-votes
  (into [] (for [[id t d] features]
             (let [c (merge {:user 0 :total 0} (combined-counts id))]
               [id t d (:total c) (:user c)]))))

(defc= sort-order
  (->> (map vector features-with-votes (iterate inc 0))
       (sort-by #(get-in % [0 3]))
       (reverse)
       (map #(get % 1))))

(defc= sorted-features
  (map #(get features-with-votes %) sort-order))

(defc= votes-remaining
  (let [max-votes (ffirst (d/q '[:find ?m
                                 :in $ ?u
                                 :where
                                 [?u :user/max-votes ?m]] db current-user-id))
        current-votes (ffirst (d/q '[:find (count ?v)
                                     :in $ ?u
                                     :where
                                     [?v :vote/user ?u]] db current-user-id))]
    (- max-votes current-votes)))

; --------------------------
; Commands
; --------------------------

(defn do-cmd! [cmd & args]
  (match [cmd args]

         [:up-vote ([db feature user] :seq)]
         (rpc/add-vote feature user)


         [:down-vote ([db feature user] :seq)]
         (when-let [v (ffirst (d/q '[:find ?e
                                     :in $ ?feature ?user
                                     :where
                                     [?e :vote/feature ?feature]
                                     [?e :vote/user ?user]] db feature user))]
           (rpc/remove-vote v))

         [:add-feature ([db title description] :seq)]
         (rpc/add-feature title description)

         [:login ([db username] :seq)]
         (when-let [u (ffirst (d/q '[:find ?u
                                     :in $ ?n
                                     :where [?u :user/name ?n]] db username))]
           (reset! current-page (ffirst @pages))
           (reset! current-user-id u))

         [:logout ([db] :seq)]
         (do (reset! current-page nil)
           (reset! current-user-id nil))

         [:set-page ([db id] :seq)]
         (reset! current-page id)

         :else (println "Command not recognised: " cmd *mode* args)))

; --------------------------
; Page
; --------------------------

(html
 (head
  (link :rel "stylesheet" :type "text/css" :href "css/main.css"))
 (body
  (div :class "ui inverted fixed black main menu"
       (div :class "ui simple dropdown item" "CAST"
            (div :class "menu"
                 :do-toggle logged-in?
                 (loop-tpl :bindings [[id page-name] pages]
                           :reverse true
                           (div :class "item"
                                :on-click #(do-cmd! :set-page @id) page-name))))

       (div :class "right menu"
            :do-toggle logged-in?
            (div :class "item"
                 (text "~{votes-remaining} votes left"))
            (div :class "ui simple dropdown item"
                 (text "~{current-user-name}")
                 (div :class "menu"
                      (div :class "item" :on-click #(do-cmd! :logout) "Log out")))))

  (div :class "container"
       (div :class "ui form segment"
            :do-toggle (cell= (not logged-in?))
            (div :class "field"
                 (label "Username")
                 (div :class "ui left labeled icon input"
                      (input :type "text" :id "username" :placeholder "Username")
                      (i :class "user icon")
                      (div :class "ui corner label"
                           (i :class "icon asterisk"))))
            (div :class "field"
                 (label "Password")
                 (div :class "ui left labeled icon input"
                      (input :type "password" :id "password")
                      (i :class "lock icon")
                      (div :class"ui corner label"
                           (i :class "icon asterisk"))))
            (div :class "ui teal submit button"
                 :on-click #(do-cmd! :login (val-id "username") (val-id "password"))
                 "Login"))

       (div :do-toggle logged-in?
            (loop-tpl :bindings [[id title description votes my-votes] sorted-features]
                      (let [votes-available (cell= (> votes-remaining 0))
                            voted-for-this (cell= (> my-votes 0))]
                        (div :class "feature ui inverted black segment"
                             (div :class "votes ui raised teal segment"
                                  (span (text "~{votes}")))
                             (div :class "details"
                                  (div :class "ui bottom right attached black inverted label"
                                       (div :class "ui mini buttons"
                                            (div :class "ui icon green button"
                                                 :on-click #(when @votes-available (do-cmd! :up-vote @id @current-user-id))
                                                 (i :class "icon plus")
                                                 (span :do-toggle voted-for-this
                                                       (text "~{my-votes}")))
                                            (div :class "ui icon red button"
                                                 :on-click #(do-cmd! :down-vote @id @current-user-id)
                                                 (i :class "icon minus"))))
                                  (div :class "ui header" (text "~{title}"))
                                  (div :class "ui clearing divider")
                                  (p (text "~{description}"))))))))))

